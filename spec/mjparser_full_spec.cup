

package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;


parser code {:

	Logger log = Logger.getLogger(getClass());
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }


:}


scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

/* keywords */
terminal PROG, PRINT, READ;					/* builtin func */
terminal WHILE, FOREACH; 					/* loop keyword	*/
terminal IF, ELSE, CONTINUE, BREAK, RETURN;	/* control flow keywords */
terminal CLASS, EXTENDS, NEW; 				/* class keywords */

/* types */
/*terminal INT, CHAR, BOOL;*/
terminal VOID;

/* operators */
terminal INC, DEC, PLUS, MINUS, STAR, DIV, MOD;
terminal REL_EQUAL, REL_NOT_EQUAL, GT, GTE, LT, LTE, AND, OR;
terminal EQUAL;
terminal SEMICOL, COL, COMMA, DOT, LPAREN, RPAREN, LSQUARE, RSQUARE, LBRACE, RBRACE, ARROW;

/* const */
terminal CONST;
terminal NULL;  

terminal String IDENT;
terminal Integer NUMERIC_CONST;
terminal String STR_CONST;
terminal Character CHAR_CONST;
terminal TRUE;
terminal FALSE;
/*
nonterminal Program, VarDeclList, MethodDeclList, VarDecl, Type, MethodDecl;
nonterminal FormPars, StatementList, FormalParamList, FormalParamDecl, Statement;
nonterminal Designator, Expr, Term, Addop, Factor, ActualPars, ActualParamList;
nonterminal Matched, Unmatched;

(ClassGlobalDecl) ClassDecl |
*/

nonterminal Prog, GlobalDeclList, GlobalDecl, VarDecl, ConstDecl, VarDeclList;
nonterminal Type, Var, VarList, ConstVar, ConstVarList, ConstVal;
nonterminal MethodDeclList, MethodDecl, FormalPar, FormalParList, FormalPars;
nonterminal Statement, StatementList, MatchedStatement, UnmatchedStatement, Condition, ConditionTerm, ConditionFactor;
nonterminal Expr, ReturnExpr, PrintExpr, AssignOp, RelOp, AddOp, MulOp;
nonterminal Factor, Term, BaseExpr;
nonterminal Designator, DesignatorStatement, DesignatorStatementUnpackPart, DesignatorStatementUnpackList;
nonterminal ActualParList, ActualPars;
nonterminal BoolConstVal; 
nonterminal ClassDecl, ConstructorDecl;

Prog ::= (Prog) PROG IDENT GlobalDeclList LBRACE MethodDeclList RBRACE;

GlobalDeclList ::= (GlobalDecls) GlobalDeclList GlobalDecl |
				   (NoDeclsGlobal) /* epsilon */
				   ;

GlobalDecl ::= 	(GlobalVarDecl) VarDecl |
		  		(GlobalConstDecl) ConstDecl |
		  		(GlobalClassDecl) ClassDecl
		  		;

ClassDecl ::= (BaseClassNoMethodsDecl) CLASS IDENT:className LBRACE VarDeclList RBRACE |
			  (BaseClassWithMethodsDecl) CLASS IDENT:className LBRACE VarDeclList LBRACE ConstructorDecl MethodDeclList RBRACE RBRACE |
			  (ExtendedClassNoMethodsDecl) CLASS IDENT:className EXTENDS Type:baseType LBRACE VarDeclList RBRACE |
			  (ExtendedClassWithMethodsDecl) CLASS IDENT:className EXTENDS Type:baseType LBRACE VarDeclList LBRACE ConstructorDecl MethodDeclList RBRACE RBRACE
			  ;

ConstructorDecl ::= (ConstructorDecl) IDENT:constructorName LPAREN FormalPars RPAREN VarDeclList LBRACE StatementList RBRACE;
		  		
VarDecl ::= (VarDecl) Type:typeName VarList SEMICOL;

ConstDecl ::= (ConstVarDecl) CONST Type:typeName ConstVarList SEMICOL;

VarList ::= (MultiVar) VarList COMMA Var |
		 (SingleVar) Var
		 ;

Var ::= (VarScalar) IDENT:varName |
		(VarArray) IDENT:varName LSQUARE RSQUARE
		;
		
ConstVarList ::= (MultiConstVar) ConstVarList COMMA ConstVar |
			  (SingleConstVar) ConstVar
			  ;
		
ConstVar ::= (ConstVarScalar) IDENT:varName EQUAL ConstVal;

ConstVal ::= (NumericConst) NUMERIC_CONST |
			 (CharConst) CHAR_CONST |
			 (BoolConst) BoolConstVal | 
	     	 ;
	     	 
Type ::= (Type) IDENT:typeName;

BoolConstVal ::= (BoolConstTrue) TRUE |
				 (BoolConstFalse) FALSE
				 ;


MethodDeclList ::= (MethodDecls) MethodDeclList MethodDecl |
				   (NoMethods) /* epsilon */
				   ;

VarDeclList ::= (VarDecls) VarDeclList VarDecl |
		     	(NoVars) /* epsilon */
		     	;

MethodDecl ::= (TypedMethodDecl) Type:retType IDENT:methodName LPAREN FormalPars RPAREN VarDeclList LBRACE StatementList RBRACE |
			   (VoidMethodDecl) VOID IDENT:methodName LPAREN FormalPars RPAREN VarDeclList LBRACE StatementList RBRACE
			   ;

FormalPars ::= (FormalParamList) FormalParList |
				 (NoFormalParamList) /*epsilon*/
				 ;

FormalParList ::= (MultiFormalParam) FormalParList COMMA FormalPar |
					(SingleFormalParam) FormalPar
					;

FormalPar ::= (FormalParam) Type:paramType Var:varName;

StatementList ::= (StmtList) StatementList Statement |
				  (NoStmt) /* epsilon */
				  ;

Statement ::= (MatchedStmt) MatchedStatement |
			  (UnmatchedStmt) UnmatchedStatement
			  ;

UnmatchedStatement ::= (UnmatchedIfStmt) IF LPAREN Condition RPAREN Statement |
				       (UnmatchedIfElseStmt) IF LPAREN Condition RPAREN MatchedStatement ELSE UnmatchedStatement
					   ;

MatchedStatement ::= (DesignatorStmt) DesignatorStatement SEMICOL |
					 (WhileStmt) WHILE LPAREN Condition RPAREN MatchedStatement |
					 (BreakStmt) BREAK SEMICOL |
					 (ContinueStmt) CONTINUE SEMICOL |
					 (ReturnStmt) RETURN ReturnExpr SEMICOL |
					 (ReadStmt) READ LPAREN Designator RPAREN SEMICOL |
					 (PrintStmt) PRINT LPAREN PrintExpr RPAREN SEMICOL |
					 (ForeachStmt) Designator DOT FOREACH LPAREN IDENT:name ARROW Statement RPAREN SEMICOL |
					 (IfElseStmt) IF LPAREN Condition RPAREN MatchedStatement ELSE MatchedStatement |
					 (MultiStmt) LBRACE StatementList RBRACE |
					 error SEMICOL:l 
					 {: parser.report_error("sintaksna greska na liniji: " + lleft, null); :}
					 ;
					 ;

DesignatorStatement ::= (AssignStmt) Designator AssignOp Expr |
						(FuncCallStmt) Designator LPAREN ActualPars RPAREN |
						(IncStmt) Designator INC |
						(DecStmt) Designator DEC |
						(UnpackStmt) LSQUARE DesignatorStatementUnpackList RSQUARE EQUAL Designator
						;

DesignatorStatementUnpackList ::= (UnpackList) DesignatorStatementUnpackList COMMA DesignatorStatementUnpackPart |
								  (UnpackSingle) DesignatorStatementUnpackPart
								  ;

DesignatorStatementUnpackPart ::= (DesignatorPart) Designator |
								  (NoDesignatorPart) /* epsilon */
								  ;

Designator ::= (DotDesignator) Designator DOT IDENT:varName |
			   (ArrayDesignator) Designator LSQUARE Expr RSQUARE |
			   (VarDesignator) IDENT:varName
			   ;

ReturnExpr ::= (RetExpr) Expr |
			   (NoRetExpr) /* epsilon */
			   ;
			   
PrintExpr ::= (PrintExprWidth) Expr COMMA NUMERIC_CONST:width |
			  (PrintExprNoWidth) Expr
			  ;

ActualPars ::= (ActualParamList) ActualParList |
			   (NoActualParamList) /*epsilon*/
			   ;

ActualParList ::= (MultiActualParam) ActualParList COMMA Expr |
				  (SingleActualParam) Expr
				  ;


Condition ::= (MultiTermCondition) Condition OR ConditionTerm |
			  (SingleTermCondition) ConditionTerm
			  ;

ConditionTerm ::= (MultiFactorTermCondition) ConditionTerm AND ConditionFactor |
				  (SingleFactorTermCondition) ConditionFactor
				  ;
				  
ConditionFactor ::= (MultiFactor) Expr RelOp Expr |
					(SingleFactor) Expr
					;
					
Expr ::= (Expression) BaseExpr |
		 (NegatedExpression) MINUS BaseExpr
		 ;

BaseExpr ::= (AddOpExpression) BaseExpr AddOp Term |
			 (SingleTermExpression) Term
			 ;

Term ::= (MulOpTerm) Term MulOp Factor |
	     (SingleTerm) Factor
		 ;

Factor ::= (DesignatorFactor) Designator |
		   (FuncCallFactor) Designator LPAREN ActualPars RPAREN |
		   (NumConstFactor) NUMERIC_CONST |
		   (CharConstFactor) CHAR_CONST |
		   (BoolConstFactor) BoolConstVal |
		   (NewArrayFactor) NEW Type:objType LSQUARE Expr RSQUARE |
		   (NewObjectFactor) NEW Type:objType LPAREN ActualPars RPAREN |
		   (GroupedFactor) LPAREN Expr RPAREN
		   ;
		   
AssignOp ::= (AssignOp) EQUAL;

RelOp ::= (RelOpEqual) REL_EQUAL |
		  (RelOpNotEqual) REL_NOT_EQUAL |
		  (RelOpGT) GT |
		  (RelOpGTE) GTE |
		  (RelOpLT) LT |
		  (RelOpLTE) LTE
		  ;

AddOp ::= (AddOpPlus) PLUS |
		  (AddOpMinus) MINUS
		  ;

MulOp ::= (MulOpTimes) STAR |
		  (MulOpDiv) DIV |
		  (MulOpMod) MOD
		  ;

		
		

